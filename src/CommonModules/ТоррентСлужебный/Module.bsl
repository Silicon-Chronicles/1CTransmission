//TODO Добавить поддержку авторизации
//TODO Добавить поддержку SSL

#Область ПрограммныйИнтерфейс

	Функция ПолучитьТелоЗапроса(Метод, Аргументы = Неопределено, Тэг = Неопределено) Экспорт
	
	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку();
	
	РезультатСтруктура = Новый Структура;
	Если ЗначениеЗаполнено(Аргументы) Тогда
		РезультатСтруктура.Вставить("arguments", Аргументы);
	КонецЕсли;
	РезультатСтруктура.Вставить("method", Метод);
	Если ЗначениеЗаполнено(Тэг) Тогда
		РезультатСтруктура.Вставить("tag", Тэг);
	Иначе
		РезультатСтруктура.Вставить("tag", 1);
	КонецЕсли;
	
	ЗаписатьJSON(ЗаписьJSON, РезультатСтруктура);
	
	Возврат ЗаписьJSON.Закрыть();	
	
КонецФункции

Функция ОтправитьЗапрос(ТелоЗапроса, ПрочитатьВСоответствие = Ложь) Экспорт
	ПараметрыПреобразования = Новый Структура("ПрочитатьВСоответствие", ПрочитатьВСоответствие);
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("X-Transmission-Session-Id", ПараметрыСеанса.ТР_ИдентификаторСессии);
	ДополнительныеПараметры = Новый Структура("Заголовки", Заголовки);
	
	Результат = КоннекторHTTP.Post(ПолучитьАдресПодключения(), ТелоЗапроса, ДополнительныеПараметры);
	Если Результат.КодСостояния = 409 Тогда
		ПараметрыСеанса.ТР_ИдентификаторСессии = Результат.Заголовки["X-Transmission-Session-Id"];
		Заголовки.Вставить("X-Transmission-Session-Id", ПараметрыСеанса.ТР_ИдентификаторСессии);
		Результат = КоннекторHTTP.Post(ПолучитьАдресПодключения(), ТелоЗапроса, ДополнительныеПараметры);
		Если Результат.КодСостояния = 200 Тогда
			Возврат КоннекторHTTP.КакJson(Результат, ПараметрыПреобразования);	
		Иначе
			ВызватьИсключение КоннекторHTTP.КакТекст(Результат);
		КонецЕсли;
	ИначеЕсли Результат.КодСостояния = 200 Тогда
		Возврат КоннекторHTTP.КакJson(Результат, ПараметрыПреобразования);	
	Иначе
		ВызватьИсключение КоннекторHTTP.КакТекст(Результат);
	КонецЕсли;
		
КонецФункции

Функция ПолучитьАдресПодключения() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	НастройкиПодключения.Адрес,
	|	НастройкиПодключения.Порт
	|ИЗ
	|	Справочник.НастройкиПодключения КАК НастройкиПодключения
	|ГДЕ
	|	НастройкиПодключения.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ПараметрыСеанса.ТР_ТекущиеНастройки);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ВызватьИсключение НСтр("ru = 'Отсутствуют настройки подключения.'");
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий(); 
	
	Шаблон = "http://%1:%2/transmission/rpc";
	
	Возврат СтрШаблон(Шаблон, ВыборкаДетальныеЗаписи.Адрес, ВыборкаДетальныеЗаписи.Порт);
	
КонецФункции

#КонецОбласти